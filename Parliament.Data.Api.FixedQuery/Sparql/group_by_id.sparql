PREFIX : @schemaUri

CONSTRUCT {
    ?group
        a :Group, ?class, ?parliamentaryCommittee, ?selectCommittee ;
        :groupName ?groupName ;
        :groupStartDate ?groupStartDate ;
        :formalBodyHasParentFormalBody ?parentformalBody ;
        :formalBodyHasChildFormalBody ?childformalBody ;
        :formalBodyRemit ?bodyRemit ;
        :formalBodyHasLeadHouse ?leadHouse ;
        :formalBodyName ?formalBodyName ;
        :formalBodyStartDate ?formalBodyStartDate ;
        :formalBodyHasContactPoint ?formalBodyContactPoint ;
        :formalBodyHasFormalBodyMembership ?formalBodyMembership ;
        :formalBodyHasHouse ?formalBodyHouse ;
        :formalBodyHasFormalBodyChair ?bodyChair ;
        :memberCount ?memberCount ;
    .
    ?formalBodyMembership
        a :FormalBodyMembership, ?exOfficioMembership, ?alternateMembership ;
        :formalBodyMembershipHasPerson ?formalBodyPerson ;
    .
    ?formalBodyContactPoint
        a :ContactPoint ;
        :email ?email ;
        :phoneNumber ?phoneNumber ;
    .
    ?formalBodyHouse
        a :House ;
        :houseName ?formalBodyHouseName ;
        :houseHasContactPoint ?formalBodyHouseContactPoint ;
    .
    ?leadHouse
        a :House ;
        :houseName ?leadHouseName ;
        :houseHasContactPoint ?leadHouseContactPoint ;
    .
    ?house
        a :House ;
        :houseName ?houseName ;
    .
    ?leadHouseContactPoint
        a :ContactPoint ;
        :contactPointHasPostalAddress ?postalAddress ;
    .
    ?formalBodyHouseContactPoint
        a :ContactPoint ;
        :contactPointHasPostalAddress ?postalAddress ;
    .
    ?postalAddress
        a :PostalAddress ;
        :addressLine1 ?address1 ;
        :addressLine2 ?address2 ;
        :addressLine3 ?address3 ;
        :addressLine4 ?address4 ;
        :addressLine5 ?address5 ;
        :postCode ?postCode ;
    .
    ?formalBodyPerson
        a :Person ;
        :personFamilyName ?formalBodyPersonFamilyName ;
        :personGivenName ?formalBodyPersonGivenName ;
        <http://example.com/F31CBD81AD8343898B49DC65743F0BDF> ?formalBodyPersonDisplayAs ;
        <http://example.com/A5EE13ABE03C4D3A8F1A274F57097B6C> ?formalBodyPersonListAs ;
        :memberHasMemberImage ?image ;
        :memberHasParliamentaryIncumbency ?parlIncumbency ;
        :partyMemberHasPartyMembership ?partyMembership ;
    .
    ?image
        a :MemberImage;
    .
    ?parlIncumbency
        a :SeatIncumbency ;
        :seatIncumbencyHasHouseSeat ?houseSeat ;
        :parliamentaryIncumbencyStartDate ?parlincumbencyStartDate ;
        :parliamentaryIncumbencyEndDate ?parlIncumbencyEndDate ;
    .
    ?houseSeat
        a :HouseSeat ;
        :houseSeatHasConstituencyGroup ?constituencyGroup ;
        :houseSeatHasHouse ?house ;
    .
    ?constituencyGroup
        a :ConstituencyGroup ;
        :constituencyGroupName ?constituencyGroupName ;
    .
    ?partyMembership
        a :PartyMembership ;
        :partyMembershipHasParty ?party ;
    .
    ?party
        a :Party ;
        :partyName ?partyName ;
    .
    ?bodyChair
        a :Position ;
        :positionHasIncumbency ?bodyChairIncumbency ;
    .
    ?bodyChairIncumbency
        a :Incumbency ;
        :incumbencyStartDate ?bodyChairIncumbencystartDate ;
        :incumbencyHasPerson ?formalBodyChairperson ;
    .
}
WHERE {
    {
        SELECT *
        WHERE {
            BIND(@group_id AS ?group)

            ?group
                a :Group ;
                a ?class ;
            .

            OPTIONAL { ?group :groupName ?groupName . }
            OPTIONAL { ?group :groupStartDate ?groupStartDate . }
            OPTIONAL { ?group :formalBodyHasParentFormalBody ?parentformalBody . }
            OPTIONAL { ?group :formalBodyHasChildFormalBody ?childformalBody . }
            OPTIONAL { ?group :formalBodyRemit ?bodyRemit . }

            OPTIONAL {
                OPTIONAL {
                    ?group :formalBodyHasLeadHouse ?leadHouse .

                    ?leadHouse
                        :houseName ?leadHouseName ;
                        :houseHasContactPoint ?leadHouseContactPoint ;
                    .

                    ?leadHouseContactPoint :contactPointHasPostalAddress ?postalAddress .

                    OPTIONAL { ?postalAddress :addressLine1 ?address1 . }
                    OPTIONAL { ?postalAddress :addressLine2 ?address2 . }
                    OPTIONAL { ?postalAddress :addressLine3 ?address3 . }
                    OPTIONAL { ?postalAddress :addressLine4 ?address4 . }
                    OPTIONAL { ?postalAddress :addressLine5 ?address5 . }
                    OPTIONAL { ?postalAddress :postCode ?postCode . }
                }

                ?group :formalBodyName ?formalBodyName .
                OPTIONAL { ?group :formalBodyStartDate ?formalBodyStartDate . }
                OPTIONAL {
                    ?group :formalBodyHasHouse ?formalBodyHouse .
                    ?formalBodyHouse :houseName ?formalBodyHouseName .
                }

                OPTIONAL {
                    ?formalBodyHouse :houseHasContactPoint ?formalBodyHouseContactPoint .
                    ?formalBodyHouseContactPoint :contactPointHasPostalAddress ?postalAddress .

                    OPTIONAL { ?postalAddress :addressLine1 ?address1 . }
                    OPTIONAL { ?postalAddress :addressLine2 ?address2 . }
                    OPTIONAL { ?postalAddress :addressLine3 ?address3 . }
                    OPTIONAL { ?postalAddress :addressLine4 ?address4 . }
                    OPTIONAL { ?postalAddress :addressLine5 ?address5 . }
                    OPTIONAL { ?postalAddress :postCode ?postCode . }
                }

                OPTIONAL {
                    ?group :formalBodyHasContactPoint ?formalBodyContactPoint .

                    OPTIONAL { ?formalBodyContactPoint :email ?email . }
                    OPTIONAL { ?formalBodyContactPoint :phoneNumber ?phoneNumber . }
                }

                OPTIONAL {
                    ?group :formalBodyHasFormalBodyMembership ?formalBodyMembership .

                    FILTER NOT EXISTS { ?formalBodyMembership a :PastFormalBodyMembership . }

                    OPTIONAL {
                        BIND(:ExOfficioMembership AS ?exOfficioMembership)

                        ?formalBodyMembership a ?exOfficioMembership .
                    }

                    OPTIONAL {
                        BIND(:AlternateMembership AS ?alternateMembership)

                        ?formalBodyMembership a ?alternateMembership .
                    }

                    OPTIONAL {
                        BIND(:ParliamentaryCommittee AS ?parliamentaryCommittee)

                        ?group a ?parliamentaryCommittee .
                    }

                    OPTIONAL {
                        BIND(:SelectCommittee AS ?selectCommittee)

                        ?group a ?selectCommittee .
                    }

                    ?formalBodyMembership :formalBodyMembershipHasPerson ?formalBodyPerson .

                    OPTIONAL {
                        ?formalBodyPerson
                            a :Person ;
                            :personFamilyName ?formalBodyPersonFamilyName ;
                            :personGivenName ?formalBodyPersonGivenName ;
                            <http://example.com/F31CBD81AD8343898B49DC65743F0BDF> ?formalBodyPersonDisplayAs ;
                            <http://example.com/A5EE13ABE03C4D3A8F1A274F57097B6C> ?formalBodyPersonListAs ;
                        .

                        OPTIONAL { ?formalBodyPerson :memberHasMemberImage ?image . }
                        OPTIONAL {
                            ?formalBodyPerson :memberHasParliamentaryIncumbency ?parlIncumbency    .
                            ?parlIncumbency
                                a :SeatIncumbency ;
                                :seatIncumbencyHasHouseSeat ?houseSeat ;
                                :parliamentaryIncumbencyStartDate ?parlincumbencyStartDate ;
                            .

                            OPTIONAL { ?parlIncumbency :parliamentaryIncumbencyEndDate ?parlIncumbencyEndDate } .
                            OPTIONAL {
                                ?houseSeat :houseSeatHasHouse ?house .
                                ?house :houseName ?houseName .
                            }
                            OPTIONAL {
                                ?houseSeat :houseSeatHasConstituencyGroup ?constituencyGroup .
                                ?constituencyGroup
                                    a :ConstituencyGroup ;
                                    :constituencyGroupName ?constituencyGroupName ;
                                .

                                FILTER NOT EXISTS { ?constituencyGroup :constituencyGroupEndDate ?constituencygroupEndDate . }
                            }
                        }
                        OPTIONAL {
                            ?formalBodyPerson :partyMemberHasPartyMembership ?partyMembership .
                            ?partyMembership
                                a :PartyMembership ;
                                :partyMembershipHasParty ?party ;
                            .
                            ?party
                                a :Party ;
                                :partyName ?partyName ;
                            .

                            FILTER NOT EXISTS { ?partyMembership :partyMembershipEndDate ?partyMembershipEndDate . }
                        }
                    }
                }

                OPTIONAL {
                    ?group :formalBodyHasFormalBodyChair ?bodyChair .
                    ?bodyChair :positionHasIncumbency ?bodyChairIncumbency .
                    ?bodyChairIncumbency
                        :incumbencyStartDate ?bodyChairIncumbencystartDate ;
                        :incumbencyHasPerson ?formalBodyChairperson ;
                    .

                    FILTER NOT EXISTS { ?bodyChairIncumbency :incumbencyEndDate ?bodyChairIncumbencyEndDate } .
                }
            }
        }
    }
    UNION
    {
        SELECT ?group (COUNT(?formalBodyMembership) AS ?memberCount)
        WHERE {
            BIND(@group_id AS ?group)

            ?group :formalBodyHasFormalBodyMembership ?formalBodyMembership .

            FILTER NOT EXISTS { ?formalBodyMembership a :PastFormalBodyMembership } .
        }
        GROUP BY ?group
    }
}
