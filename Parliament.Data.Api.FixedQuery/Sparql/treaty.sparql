PREFIX : @schemaUri

CONSTRUCT {
    ?treaty
        a :InForceTreaty, :LaidThing, :Treaty, :WorkPackagedThing ;
        :inForceTreatyHasTreatySeriesMembership ?treatySeriesMembership ;
        :laidThingHasLaying ?laying ;
        :treatyCommandPaperNumber ?treatyCommandPaperNumber ;
        :treatyCommandPaperPrefix ?treatyCommandPaperPrefix ;
        :treatyName ?treatyName ;
        :treatyHasCountrySeriesMembership ?countrySeriesMembership ;
        :treatyHasEuropeanUnionSeriesMembership ?europeanSeriesMembership ;
        :treatyHasLeadGovernmentOrganisation ?leadGovernmentOrganisation ;
        :treatyHasMiscellaneousSeriesMembership ?miscellaneousSeriesMembership ;
        :treatyHasSeriesMembership ?seriesMembership ;
        :workPackagedThingHasWorkPackage ?workPackage ;
        :workPackagedThingHasWorkPackagedThingWebLink ?workPackagedThingWebLink .

    ?leadGovernmentOrganisation
        a :GovernmentOrganisation ;
        :groupName ?leadGovernmentOrganisationGroupName .

    ?laying
        a :Laying, :BusinessItem ;
        :layingDate ?layingDate ;
        :layingHasLayingBody ?layingBody ;
        :businessItemHasProcedureStep ?procedureStep .

    ?layingBody
        a :Group ;
        :groupName ?layingBodyGroupName .

    ?workPackage
        a :WorkPackage ;
        :workPackageHasBusinessItem ?businessItem ;
        :workPackageHasProcedure ?procedure .

    ?businessItem
        a :BusinessItem ;
        :businessItemDate ?businessItemDate ;
        :businessItemHasBusinessItemWebLink ?businessItemWebLink ;
        :businessItemHasProcedureStep ?BusinessItemProcedureStep .

    ?BusinessItemProcedureStep
        a :ProcedureStep ;
        :procedureStepName ?procedureStepName .

    ?procedure
        a :Procedure ;
        :procedureName ?procedureName .

    ?procedureStep
        a :ProcedureStep ;
        :procedureStepHasHouse ?house .

    ?seriesMembership
        a ?seriesMembershipType ;
        :seriesItemCitation ?seriesItemCitation .

    ?house
        a :House ;
        :houseName ?houseName .
}
WHERE {
    ?treaty
        a :Treaty ;
        :treatyName ?treatyName ;
        :treatyHasLeadGovernmentOrganisation ?leadGovernmentOrganisation .
    OPTIONAL {
        ?treaty
            :treatyCommandPaperPrefix ?treatyCommandPaperPrefix ;
            :treatyCommandPaperNumber ?treatyCommandPaperNumber .
    }

    OPTIONAL {
        ?treaty
            a :InForceTreaty ;
            :inForceTreatyHasTreatySeriesMembership ?treatySeriesMembership .
    }

    ?leadGovernmentOrganisation :groupName ?leadGovernmentOrganisationGroupName .

    OPTIONAL {
        ?treaty :treatyHasSeriesMembership ?seriesMembership .
        ?seriesMembership
            a ?seriesMembershipType ;
            :seriesItemCitation ?seriesItemCitation .
    }

    OPTIONAL { ?treaty :treatyHasEuropeanUnionSeriesMembership ?europeanSeriesMembership . }

    OPTIONAL { ?treaty :treatyHasMiscellaneousSeriesMembership ?miscellaneousSeriesMembership . }

    OPTIONAL { ?treaty :treatyHasCountrySeriesMembership ?countrySeriesMembership . }

    OPTIONAL {
        ?treaty
            a :LaidThing ;
            :laidThingHasLaying ?laying .

        ?laying
            :businessItemHasProcedureStep ?procedureStep ;
            :layingDate ?layingDate ;
            :layingHasLayingBody ?layingBody .

        ?layingBody :groupName ?layingBodyGroupName .

        ?procedureStep :procedureStepHasHouse ?house .

        ?house :houseName ?houseName .
    }

    OPTIONAL {
        ?treaty
            a :WorkPackagedThing ;
            :workPackagedThingHasWorkPackage ?workPackage .

        ?workPackage :workPackageHasProcedure ?procedure .

        ?procedure :procedureName ?procedureName .

        OPTIONAL { ?treaty :workPackagedThingHasWorkPackagedThingWebLink ?workPackagedThingWebLink . }

        OPTIONAL {
            ?workPackage :workPackageHasBusinessItem ?businessItem .

            ?businessItem :businessItemHasProcedureStep ?BusinessItemProcedureStep .

            OPTIONAL { ?businessItem :businessItemDate ?businessItemDate . }

            OPTIONAL { ?businessItem :businessItemHasBusinessItemWebLink ?businessItemWebLink . }

            ?BusinessItemProcedureStep :procedureStepName ?procedureStepName .
        }
    }
}
