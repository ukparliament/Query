PREFIX ex: <https://example.com/>
PREFIX : @schemaUri

CONSTRUCT {
	#used old model, TODO: replace names as per new model
  ?package
    a :WorkPackage ;
    :workPackageHasWorkPackageableThing ?workPackaged ;
    :workPackageHasProcedure ?procedure ;
    :workPackageHasBusinessItem ?item ;
    .

  ?workPackaged
    a :WorkPackageableThing, ?class ;
    :workPackageableThingName ?workPackagedThingName ;
    :workPackageableThingHasWorkPackageableThingWebLink ?workPackagedLink ;
    .

  ?procedure
    a :Procedure ;
    :procedureName ?procedureName ;
    .

  ?item 
    a :BusinessItem ;
    :businessItemHasProcedureStep ?step ;
    :businessItemDate ?itemDate ;
    :businessItemHasBusinessItemWebLink ?itemLink ;
    .

  ?step
    a :ProcedureStep, ex:Actualized ;
    :procedureStepName ?stepName ;
    :procedureStepHasHouse ?house ;
    :procedureStepHasBusinessItem ?item ;
    ex:ledTo ?otherStep ;
    ex:canLeadTo ?otherStep ;
    .

  ?enablingStep
    ex:enables ?possibleStep ;
    ex:canLeadTo ?possibleStep ;
    .

  ?possibleStep
    a :ProcedureStep, ex:Possible ;
    :procedureStepName ?possibleName ;
    :procedureStepHasHouse ?possibleHouse ;
    .

  ?rootStep a ex:Root .

  ?house
    a :House ;
    :houseName ?houseName ;
    .

  ?possibleHouse
    a :House ;
    :houseName ?possibleHouseName ;
    .
}
WHERE {
  BIND(@work_package_id AS ?package)

  ?package :workPackageHasProcedure ?procedure .

  ?procedure :procedureName ?procedureName .

  OPTIONAL {
    ?package :workPackageHasWorkPackagedThing ?workPackaged . 
    
    ?workPackaged a ?class .
        
    FILTER(?class = :StatutoryInstrumentPaper || ?class = :ProposedNegativeStatutoryInstrumentPaper || ?class = :Treaty)

    OPTIONAL { ?workPackaged :workPackagedThingName ?workPackagedThingName . }
    OPTIONAL { ?workPackaged :workPackagedThingHasWorkPackagedThingWebLink ?workPackagedLink . }
  }
  
  OPTIONAL {
      {
        ?enablingStep :procedureStepHasBusinessItem/:businessItemHasWorkPackage ?package .
    
        ?possibleRoute :procedureRouteHasProcedure ?procedure .
        ?possibleRoute :procedureRouteIsFromProcedureStep ?enablingStep .
        ?possibleRoute :procedureRouteIsToProcedureStep ?possibleStep .
    
        ?possibleStep :procedureStepName ?possibleName .
    
        OPTIONAL {
          ?possibleStep :procedureStepHasHouse ?possibleHouse .
          ?possibleHouse :houseName ?possibleHouseName .
        }
    
        FILTER NOT EXISTS {
          ?possibleStep :procedureStepHasBusinessItem/:businessItemHasWorkPackage ?package .
        }
    
        FILTER NOT EXISTS {
          ?precludingRoute :procedureRouteHasProcedure ?procedure .
          ?precludingRoute :procedureRouteIsToProcedureStep ?possibleStep .
          ?precludingRoute :precludedProcedureRouteIsPrecludedByProcedureStep/:procedureStepHasBusinessItem/:businessItemHasWorkPackage ?package .
        }
    
        FILTER NOT EXISTS {
          ?requiringRoute :procedureRouteHasProcedure ?procedure .
          ?requiringRoute :requiredProcedureRouteIsRequiredByProcedureStep ?possibleStep .
    
          FILTER NOT EXISTS {
            ?requiringRoute :procedureRouteIsToProcedureStep/:procedureStepHasBusinessItem/:businessItemHasWorkPackage ?package .
          }
        }
      }
      UNION 
      {
        ?item :businessItemHasWorkPackage ?package .
    
        OPTIONAL { ?item :businessItemDate ?itemDate }
        OPTIONAL { ?item :businessItemHasBusinessItemWebLink ?itemLink }
    
        ?step :procedureStepHasBusinessItem ?item .
        ?step :procedureStepName ?stepName .
    
        OPTIONAL {
          ?step :procedureStepHasHouse ?house .
          ?house :houseName ?houseName .
        }
    
        OPTIONAL {
          ?actualizedRoute :procedureRouteHasProcedure ?procedure .
          ?actualizedRoute :causedProcedureRouteIsCausedByProcedureStep|:allowedProcedureRouteIsAllowedByProcedureStep ?step .
          ?actualizedRoute :procedureRouteIsToProcedureStep ?otherStep .
    
          ?otherStep :procedureStepHasBusinessItem/:businessItemHasWorkPackage ?package .
    
          FILTER (?otherStep != ?step)
        }
      }
      UNION 
      {
        ?rootStep :procedureStepHasBusinessItem/:businessItemHasWorkPackage ?package .
    
        OPTIONAL {
          ?higherRoute :procedureRouteHasProcedure ?procedure .
          ?higherRoute (:causedProcedureRouteIsCausedByProcedureStep|:allowedProcedureRouteIsAllowedByProcedureStep)/:procedureStepHasBusinessItem/:businessItemHasWorkPackage ?package .
          ?higherRoute :procedureRouteIsToProcedureStep ?rootStep .
        }
    
        FILTER(!BOUND(?higherRoute))
      }
  }
}
