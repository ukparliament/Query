PREFIX : @schemaUri
CONSTRUCT {
    ?person
        a :Person ;
        :personDateOfBirth ?dateOfBirth ;
        :personGivenName ?givenName ;
        :personOtherNames ?otherName ;
        :personFamilyName ?familyName ;
        <http://example.com/F31CBD81AD8343898B49DC65743F0BDF> ?displayAs ;
        <http://example.com/D79B0BAC513C4A9A87C9D5AFF1FC632F> ?fullTitle ;
        :memberHasMemberImage ?image ;
        :partyMemberHasPartyMembership ?partyMembership ;
        :memberHasIncumbency ?incumbency ;
        :personHasFormalBodyMembership ?formalBodyMembership .
    ?image
        a :MemberImage .
    ?contactPoint
        a :ContactPoint ;
        :email ?email ;
        :phoneNumber ?phoneNumber ;
        :contactPointHasPostalAddress ?postalAddress .
    ?postalAddress
        a :PostalAddress ;
        :addressLine1 ?addressLine1 ;
        :addressLine2 ?addressLine2 ;
        :addressLine3 ?addressLine3 ;
        :addressLine4 ?addressLine4 ;
        :addressLine5 ?addressLine5 ;
        :faxNumber ?faxNumber ;
        :postCode ?postCode .
    ?constituency
        a :ConstituencyGroup ;
        :constituencyGroupName ?constituencyName ;
        :constituencyGroupStartDate ?constituencyStartDate ;
        :constituencyGroupEndDate ?constituencyEndDate .
    ?seatIncumbency
        a :SeatIncumbency ;
        :incumbencyEndDate ?seatIncumbencyEndDate ;
        :incumbencyStartDate ?seatIncumbencyStartDate ;
        :seatIncumbencyHasHouseSeat ?seat ;
        :incumbencyHasContactPoint ?contactPoint .
    ?houseIncumbency
        a :HouseIncumbency ;
        :incumbencyEndDate ?houseIncumbencyEndDate ;
        :incumbencyStartDate ?houseIncumbencyStartDate ;
        :houseIncumbencyHasHouse ?house1 ;
        :incumbencyHasContactPoint ?contactPoint .
    ?seat
        a :HouseSeat ;
        :houseSeatHasConstituencyGroup ?constituency ;
        :houseSeatHasHouse ?house2 .
    ?party
        a :Party ;
        :partyName ?partyName .
    ?partyMembership
        a :PartyMembership ;
        :partyMembershipStartDate ?partyMembershipStartDate ;
        :partyMembershipEndDate ?partyMembershipEndDate ;
        :partyMembershipHasParty ?party .
    ?house1
        a :House ;
        :houseName ?houseName1 .
    ?house2
        a :House ;
        :houseName ?houseName2 .
    ?formalBodyMembership
        a :FormalBodyMembership ;
        :formalBodyMembershipStartDate ?formalBodyMembershipStartDate ;
        :formalBodyMembershipEndDate ?formalBodyMembershipEndDate ;
        :formalBodyMembershipHasFormalBody ?formalBody .
    ?formalBody
    	a :FormalBody ;
    	:formalBodyName ?formalBodyName .
}
WHERE {
    BIND(@id AS ?person)
    ?person a :Person .
    OPTIONAL { ?person :personGivenName ?givenName } .
    OPTIONAL { ?person :personOtherNames ?otherName } .
    OPTIONAL { ?person :personFamilyName ?familyName } .
    OPTIONAL { ?person <http://example.com/F31CBD81AD8343898B49DC65743F0BDF> ?displayAs } .
    OPTIONAL { ?person <http://example.com/D79B0BAC513C4A9A87C9D5AFF1FC632F> ?fullTitle } .
    OPTIONAL { ?person :memberHasMemberImage ?image } .
    OPTIONAL {
        ?person :memberHasIncumbency ?incumbency .
        OPTIONAL {
            ?incumbency a :HouseIncumbency .
            BIND(?incumbency AS ?houseIncumbency)
            ?houseIncumbency :houseIncumbencyHasHouse ?house1 .
            ?house1 :houseName ?houseName1 .
            ?houseIncumbency :incumbencyStartDate ?houseIncumbencyStartDate .
            OPTIONAL { ?houseIncumbency :incumbencyEndDate ?houseIncumbencyEndDate . }
        }
        OPTIONAL {
            ?incumbency a :SeatIncumbency .
            BIND(?incumbency AS ?seatIncumbency)
            ?seatIncumbency :seatIncumbencyHasHouseSeat ?seat .
            ?seat :houseSeatHasConstituencyGroup ?constituency .
            ?seat :houseSeatHasHouse ?house2 .
            ?house2 :houseName ?houseName2 .
            ?constituency :constituencyGroupName ?constituencyName .
            ?constituency :constituencyGroupStartDate ?constituencyStartDate .
            OPTIONAL { ?constituency :constituencyGroupEndDate ?constituencyEndDate . }
            ?seatIncumbency :incumbencyStartDate ?seatIncumbencyStartDate .
            OPTIONAL { ?seatIncumbency :incumbencyEndDate ?seatIncumbencyEndDate . }
        }
        OPTIONAL {
            ?incumbency :incumbencyHasContactPoint ?contactPoint .
            OPTIONAL { ?contactPoint :phoneNumber ?phoneNumber . }
            OPTIONAL { ?contactPoint :email ?email . }
            OPTIONAL {
                ?contactPoint :contactPointHasPostalAddress ?postalAddress .
                OPTIONAL { ?postalAddress :addressLine1 ?addressLine1 . }
                OPTIONAL { ?postalAddress :addressLine2 ?addressLine2 . }
                OPTIONAL { ?postalAddress :addressLine3 ?addressLine3 . }
                OPTIONAL { ?postalAddress :addressLine4 ?addressLine4 . }
                OPTIONAL { ?postalAddress :addressLine5 ?addressLine5 . }
                OPTIONAL { ?postalAddress :faxNumber ?faxNumber . }
                OPTIONAL { ?postalAddress :postCode ?postCode . }
            }
        }
    }
    OPTIONAL {
        ?person :partyMemberHasPartyMembership ?partyMembership .
        ?partyMembership :partyMembershipHasParty ?party .
        ?partyMembership :partyMembershipStartDate ?partyMembershipStartDate .
        OPTIONAL { ?partyMembership :partyMembershipEndDate ?partyMembershipEndDate . }
        ?party :partyName ?partyName .
    }
    OPTIONAL {
    	?person :personHasFormalBodyMembership ?formalBodyMembership .
    	?formalBodyMembership :formalBodyMembershipStartDate ?formalBodyMembershipStartDate .
    	OPTIONAL { ?formalBodyMembership :formalBodyMembershipEndDate ?formalBodyMembershipEndDate . }
    	?formalBodyMembership :formalBodyMembershipHasFormalBody ?formalBody .
    	?formalBody :formalBodyName ?formalBodyName .
    }
}
