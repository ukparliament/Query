PREFIX ex: <https://example.com/>
PREFIX : @schemaUri

CONSTRUCT {
  ?procedure
      a :Procedure ;
      :procedureName ?procedureName ;
      :procedureHasWorkPackage ?workPackage ;
  .
  ?workPackage
      a :WorkPackage ;
      :workPackageHasWorkPackageableThing ?workPackageableThing ;
      ex:oldestBusinessItemDate ?date ;
  .
  ?workPackageableThing
      a :WorkPackageableThing ;
      :workPackageableThingName ?workPackageableThingName ;
  .
}
WHERE {
  {
      SELECT * WHERE {
        BIND (@procedure_id AS ?procedure)
        ?procedure :procedureName ?procedureName .
        
        OPTIONAL {
            ?procedure :procedureHasWorkPackage ?workPackage .
            
            # Date (if any) of oldest business item in package
          {
              SELECT ?workPackage (MIN(?itemDate) AS ?date)
              WHERE {
                  ?item :businessItemHasWorkPackage ?workPackage .

                  OPTIONAL { ?item :businessItemDate ?itemDate . }
              }
              GROUP BY ?workPackage
          }
        }
    	
        ?workPackage :workPackageHasWorkPackageableThing ?workPackageableThing .
        ?workPackageableThing :workPackageableThingName ?workPackageableThingName .
      }
  }
}
